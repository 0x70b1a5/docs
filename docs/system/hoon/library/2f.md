# 2f: noun ordering
### `++aor`

Alphabetic order

Computes whether `a` and `b` are in alphabetical order, producing a
boolean.

Accepts
-------

`a` is a noun.

`b` is a noun.

Produces
--------

A boolean atom.


Source
------

    ++  aor                                                 ::  a-order
      ~/  %aor
      |=  {a/* b/*}
      ^-  ?
      ?:  =(a b)  &
      ?.  ?=(@ a)
        ?:  ?=(@ b)  |
        ?:  =(-.a -.b)
          $(a +.a, b +.b)
        $(a -.a, b -.b)
      ?.  ?=(@ b)  &
      |-
      =+  [c=(end 3 1 a) d=(end 3 1 b)]
      ?:  =(c d)
        $(a (rsh 3 1 a), b (rsh 3 1 b))
      (lth c d)


Examples
--------

    ~zod/try=> (aor 'a' 'b')
    %.y
    ~zod/try=> (aor 'b' 'a')
    %.n
    ~zod/try=> (aor "foo" "bar")
    %.n
    ~zod/try=> (aor "bar" "foo")
    %.y
    ~zod/try=> (aor "abcdefz" "abcdefa")
    %.n
    ~zod/try=> (aor "abcdefa" "abcdefz")
    %.y
    ~zod/try=> (aor 10.000 17.000)
    %.y
    ~zod/try=> (aor 10 9)
    %.n



***
### `++dor`

Numeric order

Computes whether `a` and `b` are in ascending numeric order, producing a
boolean.

Accepts
-------

`a` is a noun.

`b` is a noun.

Produces
--------

A boolean atom.

Source
------

    ++  dor                                                 ::  d-order
      ~/  %dor
      |=  {a/* b/*}
      ^-  ?
      ?:  =(a b)  &
      ?.  ?=(@ a)
        ?:  ?=(@ b)  |
        ?:  =(-.a -.b)
          $(a +.a, b +.b)
        $(a -.a, b -.b)
      ?.  ?=(@ b)  &
      (lth a b)


Examples
--------

    ~zod/try=> (dor 1 2)
    %.y
    ~zod/try=> (dor 2 1)
    %.n
    ~zod/try=> (dor ~[1 2 3] ~[1 2 4])
    %.y
    ~zod/try=> (dor ~[1 2 4] ~[1 2 3])
    %.n
    ~zod/try=> (dor (limo ~[99 100 10.000]) ~[99 101 10.000])
    %.y
    ~zod/try=> (dor ~[99 101 10.999] (limo ~[99 100 10.000]))
    %.n



***
### `++gor`

Hash order

XX revisit

Accepts
-------

`a` is a noun.

`b` is a noun.

Produces
--------

A boolean atom.

Source
------

    ++  gor                                                 ::  g-order
      ~/  %gor
      |=  {a/* b/*}
      ^-  ?
      =+  [c=(mug a) d=(mug b)]
      ?:  =(c d)
        (dor a b)
      (lth c d)


Examples
--------

    ~zod/try=> (gor 'd' 'c')
    %.y
    ~zod/try=> 'd'
    'd'
    ~zod/try=> 'c'
    ~zod/try=> `@ud`'d'
    100
    ~zod/try=> `@ud`'c'
    99
    ~zod/try=> (mug 'd')
    1.628.185.714
    ~zod/try=> (mug 'c')
    1.712.073.811
    ~zod/try=> (gor 'd' 'c')
    %.y
    ~zod/try=> (gor 'c' 'd')
    %.n
    ~zod/try=> (gor "foo" "bar")
    %.n
    ~zod/try=> (gor (some 10) (limo [1 2 3 ~]))
    %.n



***
### `++hor`

Hash order

XX revisit

Recursive hash comparator gate.

Accepts
-------

`a` is a noun.

`b` is a noun.

Produces
--------

A boolean atom. 


Source
------

    ++  hor                                                 ::  h-order
      ~/  %hor
      |=  {a/* b/*}
      ^-  ?
      ?:  ?=(@ a)
        ?.  ?=(@ b)  &
        (gor a b)
      ?:  ?=(@ b)  |
      ?:  =(-.a -.b)
        (gor +.a +.b)
      (gor -.a -.b)
    ::


Examples
--------

    ~zod/try=> (hor . 1)
    %.n
    ~zod/try=> (hor 1 2)
    %.y
    ~zod/try=> (hor "abc" "cba")
    %.y
    ~zod/try=> (hor 'c' 'd')
    %.n



***
### `++vor`

Double hash comparator gate.

XX revisit

Accepts
-------

`a` is a noun

`b` is a noun

Produces
-------

Source
------

    ++  vor                                                 ::  v-order
      ~/  %vor
      |=  {a/* b/*}
      ^-  ?
      =+  [c=(mug (mug a)) d=(mug (mug b))]
      ?:  =(c d)
        (dor a b)
      (lth c d)


Examples
--------

    ~zod/try=> (vor 'f' 'g')
    %.y
    ~zod/try=> (vor 'a' 'z')
    %.n
    ~zod/try=> (vor 43.326 41.106)
    %.n



***
