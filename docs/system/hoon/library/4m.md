# 4m: formatting functions
### `++scot`

Render dime as cord

Renders a dime `mol` as a cord.

Accepts
-------

Produces
--------

`mol` is a `++dime`.

Source
------

    ++  scot  |=(mol/dime ~(rent co %$ mol))

Examples
--------

    ~zod/try=> (scot %p ~pillyt)
    ~.~pillyt
    ~zod/try=> `@t`(scot %p ~pillyt)
    '~pillyt'
    ~zod/try=> (scot %ux 0x12)
    ~.0x12
    ~zod/try=> `@t`(scot %ux 0x12)
    '0x12'
    ~zod/try=> (scot %if .127.0.0.1)
    ~..127.0.0.1
    ~zod/try=> `@t`(scot %if .127.0.0.1)
    '.127.0.0.1'
    ~zod/try=> (scot %ta ~.asd_a)
    ~.~.asd_a
    ~zod/try=> `@t`(scot %ta ~.asd_a)
    '~.asd_a'



***
### `++scow`

Render dime as tape

Renders `mol` as a tape.

Accepts
-------

Produces
--------

`mol` is a `++dime`.

Source
------

    ++  scow  |=(mol/dime ~(rend co %$ mol))

Examples
--------

    ~zod/try=> (scow %p ~pillyt)
    "~pillyt"
    ~zod/try=> (scow %ux 0x12)
    "0x12"
    ~zod/try=> (scow %if .127.0.0.1)
    ".127.0.0.1"
    ~zod/try=> (scow %ta ~.asd_a)
    "~.asd_a"



***
### `++slat`

Curried slaw

Produces a `gate` that parses a `term` `txt` to an atom of the
odor specified by `mod`.

Accepts
-------

Produces
--------

`mod` is a term, an atom of odor `@tas`.

`txt` is a span, an atom of odor `@ta`.

Source
------

    ++  slat  |=(mod/@tas |=(txt/@ta (slaw mod txt)))

Examples
--------

    ~zod/try=> `(unit @p)`((slat %p) '~pillyt')
    [~ ~pillyt]
    ~zod/try=> `(unit @ux)`((slat %ux) '0x12')
    [~ 0x12]
    ~zod/try=> `(unit @if)`((slat %if) '.127.0.0.1')
    [~ .127.0.0.1]
    ~zod/try=> `(unit @ta)`((slat %ta) '~.asd_a')
    [~ ~.asd_a



***
### `++slav`

Demand: parse span with input odor

Parses a span `txt` to an atom of the odor specificed by `mod`. Crashes
if it failes to parse.

Accepts
-------

Produces
--------

`mod` is a term, an atom of odor `@tas`.

`txt` is a span, an atom of odor `@ta`.

Source
------

    ++  slav  |=([mod/@tas txt/@ta] (need (slaw mod txt)))

Examples
--------

    ~zod/try=> `@p`(slav %p '~pillyt')
    ~pillyt
    ~zod/try=> `@p`(slav %p '~pillam')
    ! exit
    ~zod/try=> `@ux`(slav %ux '0x12')
    0x12
    ~zod/try=> `@ux`(slav %ux '0b10')
    ! exit
    ~zod/try=> `@if`(slav %if '.127.0.0.1')
    .127.0.0.1
    ~zod/try=> `@if`(slav %if '.fe80.0.0.202')
    ! exit
    ~zod/try=> `@ta`(slav %ta '~.asd_a')
    ~.asd_a
    ~zod/try=> `@ta`(slav %ta '~~asd-a')
    ! exit



***
### `++slaw`

Parse span to input odor

Parses a span `txt` to an atom of the odor specified by `mod`.

Accepts
-------

Produces
--------

`mod` is a term, an atom of odor `@tas`.

`txt` is a span, an atom of odor `@ta`.

Source
------

    ++  slaw
      ~/  %slaw
      |=  {mod/@tas txt/@ta}
      ^-  (unit @)
      =+  con=(slay txt)
      ?.(&(?=({$~ $$ @ @} con) =(p.p.u.con mod)) ~ [~ q.p.u.con])


Examples
--------

    ~zod/try=> `(unit @p)`(slaw %p '~pillyt')
    [~ ~pillyt]
    ~zod/try=> `(unit @p)`(slaw %p '~pillam')
    ~
    ~zod/try=> `(unit @ux)`(slaw %ux '0x12')
    [~ 0x12]
    ~zod/try=> `(unit @ux)`(slaw %ux '0b10')
    ~
    ~zod/try=> `(unit @if)`(slaw %if '.127.0.0.1')
    [~ .127.0.0.1]
    ~zod/try=> `(unit @if)`(slaw %if '.fe80.0.0.202')
    ~
    ~zod/try=> `(unit @ta)`(slaw %ta '~.asd_a')
    [~ ~.asd_a]
    ~zod/try=> `(unit @ta)`(slaw %ta '~~asd-a')
    ~



***
### `++slay`

Parse span to coin

Parses a span `txt` to the unit of a `++coin`.

Accepts
-------

Produces
--------

`txt` is a `@ta`.

Source
------

        ++  slay
          |=  txt/@ta  ^-  (unit coin)
          =+  ^=  vex
              ?:  (gth 0x7fff.ffff txt)                         ::  XX  petty cache
                ~+  ((full nuck:so) [[1 1] (trip txt)])
              ((full nuck:so) [[1 1] (trip txt)])
          ?~  q.vex
            ~
          [~ p.u.q.vex]
        ::


Examples
--------

    ~zod/try=> (slay '~pillyt')
    [~ [% p=[p=~.p q=32.819]]]
    ~zod/try=> (slay '0x12')
    [~ [% p=[p=~.ux q=18]]]
    ~zod/try=> (slay '.127.0.0.1')
    [~ [% p=[p=~.if q=2.130.706.433]]]
    ~zod/try=> `@ud`.127.0.0.1
    2.130.706.433
    ~zod/try=> (slay '._20_0w25_sam__')
    [ ~
      [ %many
        p=~[[% p=[p=~.ud q=20]] [% p=[p=~.uw q=133]] [% p=[p=~.tas q=7.168.371]]]
      ]
    ]
    ~zod/try=> `@`%sam
    7.168.371
    ~zod/try=> (slay '~0ph')
    [~ [%blob p=[1 1]]]
    ~zod/try=> 0ph
    ~ <syntax error at [1 2]>
    ~zod/try=> ~0ph
    [1 1]
    ~zod/try=> `@uv`(jam [1 1])
    0vph



***
### `++smyt`

Render path as tank

Renders the path `bon` as a `tank`, which is used for
pretty-printing.

Accepts
-------

Produces
--------

`bon` is a `++path`.

Source
------

    ++  smyt                                                ::  pretty print path
      |=  bon/path  ^-  tank
      :+  %rose  [['/' ~] ['/' ~] ~]
      (turn bon |=(a/@ [%leaf (trip a)]))
    ::

Examples
--------

    ~zod/try=> (smyt %)
    [ %rose
      p=[p="/" q="/" r="/"]
        q
      ~[ [%leaf p="~zod"]
         [%leaf p="try"] 
         [%leaf p="~2014.10.28..18.36.58..a280"]
       ]
    ]
    ~zod/try=> (smyt /as/les/top)
    [ %rose
      p=[p="/" q="/" r="/"]
      q=~[[%leaf p="as"] [%leaf p="les"] [%leaf p="top"]]
    ]



***
### `++spat`

Render path as cord

Renders a path `pax` as cord.

Accepts
-------

Produces
--------

`pax` is a `path`.

Source
------

    ++  spat  |=(pax/path (crip (spud pax)))               ::  path to cord

Examples
--------

    ~zod/try=> (spat %)
    '~zod/try/~2014.10.28..18.40.20..4287'
    ~zod/try=> (spat %/bin)
    '~zod/try/~2014.10.28..18.41.12..3bcd/bin'
    ~zod/try=> (spat /as/les/top)
    '/as/les/top'



***
### `++spud`

Render path as tape

Renders a path `pax` as tape.

Accepts
-------

Produces
--------

`pax` is a `path`.

Source
------

    ++  spud  |=(pax/path ~(ram re (smyt pax)))             ::  path to tape

Examples
--------

    ~zod/try=> (spud %)
    "~zod/try/~2014.10.28..18.40.46..e951"
    ~zod/try=> (spud %/bin)
    "~zod/try/~2014.10.28..18.41.05..16f2/bin"
    ~zod/try=> (spud /as/les/top)
    "/as/les/top"



***
### `++stab`

Parse span to path

Parsing rule. Parses a span `zep` to a static `++path`.

Accepts
-------

Produces
--------

Source
------

    ++  stab                                                ::  parse cord to path
      =+  fel=;~(pfix fas (more fas urs:ab))
      |=(zep/@t `path`(rash zep fel))


Examples
--------

    ~zod/try=> (stab '/as/lek/tor')
    /as/lek/tor
    ~zod/try=> `(pole ,@ta)`(stab '/as/lek/tor')
    [~.as [~.lek [~.tor ~]]]
    ~zod/try=> (stab '~zod/arvo/~2014.10.28..18.48.41..335f/zuse')
    ~zod/arvo/~2014.10.28..18.48.41..335f/zuse
    ~zod/try=> `(pole ,@ta)`(stab '~zod/arvo/~2014.10.28..18.48.41..335f/zuse')
    [~.~zod [~.arvo [~.~2014.10.28..18.48.41..335f [~.zuse ~]]]]
    ~zod/try=> (stab '/a/~pillyt/pals/1')
    /a/~pillyt/pals/1
    ~zod/try=> `(pole ,@ta)`(stab '/a/~pillyt/pals/1')
    [~.a [~.~pillyt [~.pals [~.1 ~]]]]


***
