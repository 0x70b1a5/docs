## 2n: functional hacks
### `++cork`

Build `f` such that `(f x) .= (b (a x))`.

`a` is a noun.

`b` is a gate.

Source
------

    ++  cork  |*([a=_,* b=gate] (corl b a))                 ::  compose forward

Examples
--------

    ~zod/try=> (:(cork dec dec dec) 20)
    17
    ~zod/try=> =mal (mo (limo a/15 b/23 ~))
    ~zod/try=> ((cork ~(got by mal) dec) %a)
    14
    ~zod/try=> ((cork ~(got by mal) dec) %b)
    22



***
### `++corl`

Gate compose

XX Revisit

`a` is a gate.

`b` is a noun.

Source
------

    ++  corl                                                ::  compose backwards
      |*  [a=gate b=_,*]
      |=  c=_+<.b
      (a (b c))

Examples
--------

    ~zod/try=> ((corl (lift bex) (slat %ud)) '2')
    [~ 4]



***
### `++curr`

Right curry

Right curry a gate, binding the tail of its sample

Accepts
-------

`a` is a [gate]().

`c` is a [noun]().

Produces
--------


Source
------

    ++  curr
      |*  [a=_|=(^ _*) c=*]
      |*  b=_+<+.a
      (a b c)

Examples
--------

    ~zod/try=> =tep (curr scan sym)
    ~zod/try=> `@t`(tep "asd")
    'asd'
    ~zod/try=> `@t`(tep "lek-om")
    'lek-om'



***
### `++cury`

Curry a gate, binding the head of its sample

Accepts
-------

`a` is a [gate]().

`b` is a [noun]().

Produces
--------

A [gate]().

Source
------

    ++  cury
      |*  [a=_|=(^ _*) b=*]
      |*  c=_+<+.a
      (a b c)

Examples
--------

    ~zod/try=> =mol (cury add 2)
    ~zod/try=> (mol 4)
    6
    ~zod/try=> (mol 7)
    9



***
### `++hard`

Demands that a specific type be produced, crashing the program is it is
not.

Source
------

    ++  hard
      |*  han=$+(* *)
      |=  fud=*  ^-  han
      ~|  %hard
      =+  gol=(han fud)
      ?>(=(gol fud) gol)

Examples
--------

    ~zod/try=> ((hard (list)) (limo [1 2 3 ~]))
    ~[1 2 3]
    ~zod/try=> ((hard ,@) (add 2 2))
    4
    ~zod/try=> ((hard ,@t) (crip "Tape to cord, bro!"))
    'Tape to cord, bro'
    ~zod/try=> ((hard tape) (crip "...Tape to cord, bro?..."))
    ! hard
    ! exit



***
### `++soft`

Politely requests a specific type to be produced, producing null if it
is not.

Source
------

    ++  soft
      |*  han=$+(* *)
      |=  fud=*  ^-  (unit han)
      =+  gol=(han fud)
      ?.(=(gol fud) ~ [~ gol])

Examples
--------

    ~zod/try=> ((soft ,%4) (add 2 2))
    [~ %4]
    ~zod/try=> ((soft ,@) (add 2 2))
    [~ 4]
    ~zod/try=> ((soft ,%5) (add 2 2))
    ~
    ~zod/try=> ((soft ,@t) (crip "Tape to cord, Woohoo!"))
    [~ 'Tape to cord, Woohoo!']
    ~zod/try=> ((soft ,@t) (trip 'Cmon man... Tape to cord? Please?!'))
    ~



***
