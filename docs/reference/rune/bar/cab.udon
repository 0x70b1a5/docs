:-  :~  navhome/'/docs/'
        next/'true'
        sort/'5'
        title/'|_ "barcab"'
    ==
;>

# `|_ "barcab"`

`[%brcb p=spec q=(map term hoon)]`: produce a door, a many-armed core
with a sample.

## Expands to

```
=|  p
|%  q
==
```

## Syntax

Regular: *1-fixed*, then *battery*.

## Discussion

A door is the general case of a gate (function).  A gate is a
door with only one arm, which has the empty name `$`.

Calling a door is like calling a gate except the caller also needs
to specify the arm to be computed.  So, for example, if you have some door `d` which contains some arm `a`, and you want to pass some argument (i.e., input value `arg`, you would call it with `~(a d arg)`.

Because gates are also doors, you can call them the same way.  To call the gate `foo` as a door, instead of `(foo baz)` we would write `~($ foo baz)`.  This
is an irregular form for `%~($ foo baz)`, [%~](../../cen/sig).

## Examples

A trivial door:

```
/~zod:dojo> =mol  |_  a=@ud
                  ++  succ  +(a)
                  ++  prev  (dec a)
                  --
/~zod:dojo> ~(succ mol 1)
2
/~zod:dojo> ~(succ mol ~(succ mol ~(prev mol 5)))
6
```

A more interesting door, from the kernel library:

```
++  ne
  |_  tig=@
  ++  d  (add tig '0')
  ++  x  ?:((gte tig 10) (add tig 87) d)
  ++  v  ?:((gte tig 10) (add tig 87) d)
  ++  w  ?:(=(tig 63) '~' ?:(=(tig 62) '-' ?:((gte tig 36) (add tig 29) x)))
  --
```

The `ne` door prints a digit in base 10, 16, 32 or 64:

```
~zod:dojo> `@t`~(x ne 12)
'c'
```
